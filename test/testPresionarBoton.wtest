import wollok.game.*
import src.personaje.*
import src.objetos.*
import src.extras.*
import src.posiciones.*
import src.gameManager.*

describe "Presionar un botón" {

    const boton = new Boton(position=game.at(1,1))

    method initialize() {
        game.clear()
        game.width(3)
        game.height(3)
        game.addVisual(personaje)
        game.addVisual(boton)
    }
    test "El botón no está presionado si nadie lo pisa" {
        assert.equals(noPresionado, boton.estado())
    }

    test "El botón está presionado si alguien lo pisa" {
        personaje.position(game.at(1,2))
        personaje.mover(abajo)
        assert.equals(presionado, boton.estado())
    }

    test "El botón deja de estar presionado cuando quién está encima se mueve" {
        const caja = new Caja(position=game.at(1,1))
        game.addVisual(caja)
        assert.equals(presionado, boton.estado())
        game.removeVisual(caja)
        assert.equals(noPresionado, boton.estado())
    }
}

describe "Comprobar fin de juego" {

    const b1 = new BotonDeColor(position=game.at(1,1), color = "azul")
    const b2 = new BotonDeColor(position=game.at(2,1), color = "negro")


    method initialize() {
        game.clear()
        game.width(3)
        game.height(3)
        gameManager.agregarBoton(b1)
        gameManager.agregarBoton(b2)
    }

    test "El nivel termina cuando las cajas están en los botones correspondientes" {
        assert.notThat(gameManager.hayCajasEnBotones())
        assert.notThat(game.hasVisual(fondoVictoria))
        game.addVisual(new CajaDeColor(position=game.at(1,1), color = "azul"))
        game.addVisual(new CajaDeColor(position=game.at(2,1), color = "negro"))
        assert.that(gameManager.hayCajasEnBotones()) // No funciona si uso comprobarFinNivel. Parece que el test no gestiona bien el schedule.
    }

    test "El nivel no termina cuando hay cajas que están en los botones que no se corresponden con sus colores" {
        game.addVisual(new CajaDeColor(position=game.at(1,1), color = "violeta"))
        game.addVisual(new CajaDeColor(position=game.at(2,1), color = "negro"))
        assert.notThat(gameManager.hayCajasEnBotones())
    }

}
