import wollok.game.*
import src.personaje.*
import src.objetos.*
import src.extras.*
import src.posiciones.*
import src.nivel.*

describe "Presionar un botón" {

    const boton = new Boton(position=game.at(1,1))

    method initialize() {
        game.clear()
        game.width(3)
        game.height(3)
        game.addVisual(personaje)
        game.addVisual(boton)
    }
    test "El botón no está presionado si nadie lo pisa" {
        assert.equals(noPresionado, boton.estado())
    }

    test "El botón está presionado si alguien lo pisa" {
        personaje.position(game.at(1,2))
        personaje.mover(abajo)
        assert.equals(presionado, boton.estado())
    }

    test "El botón deja de estar presionado cuando quién está encima se mueve" {
        const caja = new CajaNormal(position=game.at(1,1))
        game.addVisual(caja)
        assert.equals(presionado, boton.estado())
        game.removeVisual(caja)
        assert.equals(noPresionado, boton.estado())
    }
}

describe "Comprobar fin de juego" {

    const b1 = new BotonColorido(position=game.at(1,1), color = "azul")
    const b2 = new BotonColorido(position=game.at(2,1), color = "negro")


    method initialize() {
        game.clear()
        game.width(3)
        game.height(3)
        nivel.agregarBoton(b1)
        nivel.agregarBoton(b2)
    }

    test "El nivel termina cuando las cajas están en los botones correspondientes" {
        assert.notThat(nivel.hayCajasEnBotones())
        assert.notThat(game.hasVisual(fondoVictoria))
        game.addVisual(new CajaColorida(position=game.at(1,1), color = "azul"))
        game.addVisual(new CajaColorida(position=game.at(2,1), color = "negro"))
        nivel.comprobarFinNivel()
        assert.that(game.hasVisual(fondoVictoria))
    }

    test "El nivel no termina cuando hay cajas que están en los botones que no se corresponden con sus colores" {
        game.addVisual(new CajaColorida(position=game.at(1,1), color = "violeta"))
        game.addVisual(new CajaColorida(position=game.at(2,1), color = "negro"))
        nivel.comprobarFinNivel()
        assert.notThat(game.hasVisual(fondoVictoria))
    }

}
